plugins {
    id 'java-library'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'com.github.hierynomus.license'
    id 'maven-publish'
}

description "Annontations for AnnoDocimal"

group rootProject.group

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    compileOnly "org.codehaus.groovy:groovy-all:2.4.21"
}

test {
    if (!rootProject.ext.groovyVersion.startsWith("2."))
        useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom { pom ->
                rootProject.configurePom(pom, project)
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") || gradle.taskGraph.hasTask("publishToMavenLocal") }
    sign publishing.publications.mavenJava
}

license {
    header = rootProject.file("LICENSE")
    mapping("java", "SLASHSTAR_STYLE")
    mapping("groovy", "SLASHSTAR_STYLE")
    mapping("gdsl", "SLASHSTAR_STYLE")
    exclude("mockup/**")
    strictCheck(true)
}

