plugins {
    id 'java-library'
    id 'nebula.maven-publish'
    id 'nebula.javadoc-jar'
    id 'nebula.source-jar'
    id 'nebula.release'
    id 'nebula.contacts'
// id'nebula.info'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'com.github.hierynomus.license'
}

description "Annontations for AnnoDocimal"

group 'com.blackbuild.annodocimal'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    compileOnly "org.codehaus.groovy:groovy-all:2.4.21"
}

test {
    if (!rootProject.ext.groovyVersion.startsWith("2."))
        useJUnitPlatform()
}
contacts {
    'stephan@blackbuild.com' {
        moniker 'Stephan Pauxberger'
        github 'pauxus'
    }
}

publishing {
    publications {
        nebula(MavenPublication) {
            pom {
                url = "https://github.com/blackbuild/anno-docimal"
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:blackbuild/anno-docimal.git'
                    developerConnection = 'scm:git:git@github.com:blackbuild/anno-docimal.git'
                    url = 'https://github.com/blackbuild/anno-docimal'
                }
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") || gradle.taskGraph.hasTask("publishToMavenLocal") }
    sign publishing.publications.nebula
}

license {
    header = rootProject.file("LICENSE")
    mapping("java", "SLASHSTAR_STYLE")
    mapping("groovy", "SLASHSTAR_STYLE")
    mapping("gdsl", "SLASHSTAR_STYLE")
    exclude("mockup/**")
    strictCheck(true)
}

